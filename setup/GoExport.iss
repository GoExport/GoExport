; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "GoExport"
#define MyAppVersion "0.10.0"
#define MyAppPublisher "LexianDEV"
#define MyAppURL "https://goexport.lexian.dev/"
#define MyAppExeName "GoExport.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{8AE56AFA-B6B3-4455-BCAD-7D82E2051EA0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={userpf}\{#MyAppName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=..\LICENSE
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
AllowUNCPath=False
UsePreviousAppDir=False
OutputDir=..\dist
ArchitecturesAllowed=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\redist\VC_redist.x64.exe"; DestDir: {tmp}; Flags: dontcopy
;Main files
Source: "..\dist\GoExport.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\dist\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\server\*"; DestDir: "{app}\server"; Flags: ignoreversion recursesubdirs createallsubdirs
;Install required dlls
Source: "..\libs\audio_sniffer-x64.dll"; DestDir: "{sys}"; Flags: onlyifdoesntexist 64bit regserver
Source: "..\libs\screen-capture-recorder-x64.dll"; DestDir: "{sys}"; Flags: onlyifdoesntexist 64bit regserver
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\VC_redist.x64.exe"; \
  StatusMsg: "Installing VC++ 2017 Redistributable..."; \
  Parameters: "/install /quiet /norestart"; \
  Check: VCRedistRequired; \
  Flags: waituntilterminated runhidden
Filename: "{app}\{#MyAppExeName}"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"

[Code]
function VCRedistRequired: Boolean;
var
  Version: string;
begin
  // Try to read the installed VC++ 2017 runtime version from the registry
  if RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64', 'Version', Version) then
  begin
    Log('Found VC++ 2017 x64 version: ' + Version);
    // Check if the version is older than 14.14.26429.03 (your minimum version)
    Result := CompareStr(Version, '14.14.26429.03') < 0;
  end
  else
  begin
    Log('VC++ 2017 x64 not found. Will install.');
    Result := True; // Not installed at all
  end;

  // If it needs installation, extract the file so it's available for [Run]
  if Result then
  begin
    ExtractTemporaryFile('VC_redist.x64.exe');
  end;
end;
